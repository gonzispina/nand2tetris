// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Or(a=reset, b=load, out=loadOrReset);
    Or(a=loadOrReset, b=inc, out=realLoad);

    Register(in=prev, load=realLoad, out=res1, out=out);

    Inc16(in=res1, out=res2);
   	Mux16(a=res1, b=res2, sel=inc, out=res3);
   	Mux16(a=res3, b=in, sel=load, out=res4);
   	Mux16(a=res4, b=false, sel=reset, out=prev);
}